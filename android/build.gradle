allprojects {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                if (android.hasProperty("lintOptions")) {
                    lintOptions {
                        checkReleaseBuilds false
                        abortOnError false
                    }
                }
                
                // Force all subprojects to use Java 17
                if (android.hasProperty("compileOptions")) {
                    compileOptions {
                        sourceCompatibility JavaVersion.VERSION_17
                        targetCompatibility JavaVersion.VERSION_17
                    }
                }
                
                // Force Kotlin to use JVM target 17
                if (android.hasProperty("kotlinOptions")) {
                    kotlinOptions {
                        jvmTarget = '17'
                    }
                }
            }
        }
        
        // Remove -Werror from all JavaCompile tasks
        tasks.withType(JavaCompile) {
            options.compilerArgs.removeAll { it == '-Werror' }
            options.compilerArgs << '-Xlint:-options'
        }
        
        // Force all Kotlin compilation tasks to use JVM target 17
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = '17'
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
